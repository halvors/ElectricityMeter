buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT"
    }
}

apply plugin: "forge"

// Define properties file
ext.configFile = file "build.properties"

configFile.withReader {
    // Load config. It shall from now be referenced as simply config or project.config
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

version = config.minecraft_version + "-" + config.mod_version
group = "org.halvors.electrometrics"
archivesBaseName = "Electrometrics"
sourceCompatibility = "7"
targetCompatibility = "7"

minecraft {
	version = config.minecraft_version + "-" + config.forge_version
    runDir = "eclipse"
    mappings = config.mcp_mappings

    // Replace version in Reference.java
    replace "@version", config.mod_version
}

dependencies {
    compile "codechicken:CodeChickenLib:" + config.minecraft_version + "-" + config.ccl_version + ":dev"
    compile "codechicken:ForgeMultipart:" + config.minecraft_version + "-" + config.fmp_version + ":dev"
}

jar {
    manifest {
        attributes 'FMLCorePlugin': 'codechicken.core.launch.DepLoader'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
}

// Fix for resources not loading when running from IntelliJ
idea {
    module {
        inheritOutputDirs = true
    }
}

processResources {
    // This will ensure that this task is redone when this versions change.
    inputs.property "mc_version", config.minecraft_version
    inputs.property "forge_version", config.forge_version
    inputs.property "mod_version", config.mod_version
    inputs.property "fmp_version", config.fmp_version

    // Replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "*.info"
                
        // Replace version and mcversion
        expand "version":project.version, "mcversion":config.mc_version
    }

    // Copy everything else
    from(sourceSets.main.resources.srcDirs) {
        include 'assets/**/*.*'
    }
}